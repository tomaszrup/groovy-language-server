plugins {
  id "com.gradleup.shadow" version "8.3.6"
  id "java"
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

test {
  useJUnitPlatform()
}

ext.mainClass = "com.tomaszrup.groovyls.GroovyLanguageServer"

tasks.build.dependsOn tasks.shadowJar

shadowJar {
  archiveBaseName.set("groovy-language-server")

  minimize {
    // Keep dependencies that use reflection / ServiceLoader
    exclude(dependency('org.slf4j:.*'))
    exclude(dependency('org.eclipse.lsp4j:.*'))
    exclude(dependency('com.google.code.gson:gson'))
    exclude(dependency('org.apache.groovy:.*'))
  }

  // Strip bundled metadata that serves no runtime purpose
  exclude 'META-INF/maven/**'
  exclude 'META-INF/LICENSE*'
  exclude 'META-INF/NOTICE*'
  exclude 'META-INF/DEPENDENCIES'
  exclude '**/*.html'

  manifest {
    attributes "Main-Class": "com.tomaszrup.groovyls.GroovyLanguageServer"
  }
}

repositories  {
  mavenCentral()
  maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

def lsp4jVersion = "0.24.0"

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:$lsp4jVersion"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:$lsp4jVersion"
  implementation 'org.gradle:gradle-tooling-api:8.10.2'
  implementation "org.apache.groovy:groovy:4.0.26"
  implementation "org.spockframework:spock-core:2.3-groovy-4.0"
  implementation "com.google.code.gson:gson:2.13.1"
  implementation "io.github.classgraph:classgraph:4.8.179"
  implementation "org.slf4j:slf4j-api:2.0.17"
  implementation "org.slf4j:slf4j-simple:2.0.17"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.4"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.4"
}
